program d2nwchem
use typedef
use matrix_operations
implicit none
integer :: nocc
type(LsItem) :: ls
character*(80) :: inputfile
character*(80) :: outputfile
character*(86) :: movecfile
integer,parameter  :: lo=33,li=34
integer            :: nrow,ncol,flen
real(realk),allocatable  :: CMO(:)
integer            :: IOS

real(8), external  :: dnrm2



 call getarg(1,inputfile)
 call getarg(2,outputfile)


    write(6,*) 'Reading lsitem...'
    call read_lsitem_from_disk(ls)

    ls%lupri=6
  
    nocc=int(ls%input%molecule%nelectrons/2)
    write(6,*) 'nocc= ', nocc

    OPEN(UNIT=lo,FILE=trim(outputfile),STATUS='REPLACE', &
     &     FORM='FORMATTED',IOSTAT=IOS)

    write(6,*) 'Writing NWCHEM input file ', trim(outputfile) 


    write(lo,'(A)') 'start'
    write(lo,*)
    write(lo,'(A)') 'title "Input generated by d2nwchem"'
    write(lo,*)
    call print_nw_xyz(ls,lo)
    write(lo,*)
    call print_nw_libbasis(ls,lo)
    write(lo,*)
    write(lo,'(A)') 'scf'
    write(lo,'(A)') '   thresh 1.0e-8'
    write(lo,'(A)') '   sym off'
    write(lo,'(A)') '   adapt off'
    write(lo,'(A)') 'end'
    write(lo,*)
    write(lo,'(A)') 'task scf'

    CLOSE(lo)



    !Read orbitals and write movec file
    
    write(6,*) 'Reading dalton orbitals in ', trim(inputfile)

    OPEN(UNIT=li,FILE=trim(inputfile),STATUS='OLD', &
   &     FORM='UNFORMATTED',IOSTAT=IOS)

    read(li) nrow, ncol
    allocate(CMO(nrow*ncol))
    read(li) CMO

    close(li)


    movecfile=trim(outputfile)
    flen=len_trim(movecfile)
    movecfile(flen+1:flen+6)='.movec'

    write(6,*) 'Writing NWCHEM movec file: ', trim(movecfile)

    OPEN(UNIT=lo,FILE=trim(movecfile),STATUS='REPLACE', &
     &     FORM='UNFORMATTED',IOSTAT=IOS)


    write(6,*) 'CMO norm:', dnrm2(nrow*ncol,CMO,1)
    call reorder_movec(CMO,nrow,ncol,ls)
    write(6,*) 'CMO norm:', dnrm2(nrow*ncol,CMO,1)
    call write_movec(CMO,nrow,ncol,nocc,lo)

    close(lo)

    deallocate(CMO)

end program d2nwchem

subroutine reorder_movec(CMO,nrow,ncol,ls)
use typedef
    implicit none
    integer     :: nrow, ncol
    real(realk) :: CMO(nrow,ncol)!,TMP(nrow,ncol)
    type(LsItem) :: ls

    integer      :: i, nAtoms, itype, ang, j, nbf, p, norb, icharge
    integer      :: list(nrow)
    integer, parameter :: perm(7,4) = reshape((/&
     & 1,0,0,0,0,0,0, &
     & 2,1,3,0,0,0,0, & ! 0 -1 1
     & 3,2,4,1,5,0,0, & ! 0 -1 1 -2 2 
     & 4,3,5,2,6,1,7  & ! 0 -1 1 -2 2 -3 3
!   & 1,0,0,0,0,0,0, &
!   & 3,2,1,0,0,0,0, & ! 1 0 -1
!   & 5,4,3,2,1,0,0, & ! 2 1 0 -1 -2 
!   & 7,6,5,4,3,2,1  & ! 3 2 1 0 -1 -2 -3 
!    & 1,0,0,0,0,0,0, &
!    & 1,2,3,0,0,0,0, & ! -1 0 1
!    & 3,4,2,5,1,0,0, & ! 0 1 -1 2 -2
!    & 4,5,3,6,2,7,1  & ! 0 1 -1 2 -2 3 -3
!    & 1,0,0,0,0,0,0, &
!    & 2,3,1,0,0,0,0, & ! -1 0 1
!    & 3,4,2,5,1,0,0, & ! 0 1 -1 2 -2
!    & 4,5,3,6,2,7,1  & ! 0 1 -1 2 -2 3 -3
    &/),(/7,4/))

    !TMP=CMO
    list=0

    nAtoms= ls%setting%MOLECULE(1)%p%nAtoms 

    do i=1,nAtoms

    IF(LS%setting%basis(1)%p%regular%labelindex .EQ.0)THEN
       icharge = INT(ls%setting%MOLECULE(1)%p%ATOM(i)%charge) 
       itype = ls%setting%basis(1)%p%regular%chargeindex(icharge)
    ELSE
       itype = ls%setting%MOLECULE(1)%p%ATOM(i)%IDtype(1)
    ENDIF


      do ang=1,ls%setting%basis(1)%p%REGULAR%ATOMTYPE(itype)%nAngmom

      norb=ls%setting%basis(1)%p%REGULAR%ATOMTYPE(itype)%SHELL(ang)%norb

        do j=1,norb
           nbf=2*ang - 1

           p=imax(nrow,list)+1
           list(p:(p+nbf-1))= p + perm(1:nbf,ang) -1

        enddo

      enddo

    enddo

    !swap rows
    do i=1,nrow
       call dswap(ncol,CMO(i,1),nrow,CMO(list(i),1),nrow)
    enddo

 contains

 function imax(n,ivec)
     implicit none
     integer :: imax
     integer :: n, i
     integer :: ivec(n)

     imax=-HUGE(1);

     do i=1,n
       imax=max(imax,ivec(i))
     enddo

 end function imax


end subroutine reorder_movec

subroutine write_movec(CMO,nrow,ncol,nocc,lo)
use precision
    implicit none
    integer     :: nrow, ncol
    real(realk) :: CMO(nrow,ncol)
    integer     :: nocc
    integer     :: lo

    character*(110),parameter :: header='basissum32,gemomsum32,scftype20,date26 skip header'
    character*(20), parameter    :: scftype='scf'
    integer                   :: j
    


    write(lo) header
    write(lo) scftype
    write(lo) 28_8 ! lentit
    write(lo) 'movecs generated by d2nwchem' !title
    write(lo) 8_8 ! lenbas
    write(lo) 'ao basis'
    write(lo) 1_8 ! nsets
    write(lo) int8(nrow) ! nbf number of basis functions
    write(lo) int8(ncol) ! norb number of orbitals
    write(lo) (2d0,j=1,nocc), (0d0,j=1,ncol-nocc) ! occupation numbers
    write(lo) (0d0,j=1,ncol) ! eigenvalues
    do j=1,ncol
     write(lo) CMO(:,j)
    enddo
    write(lo) 0d0, 0d0


end subroutine write_movec

subroutine print_nw_libbasis(ls,lo)
use trilevel_module
    implicit none
    type(lsitem)                   :: ls
    integer                        :: lo
    TYPE(trilevel_atominfo)        :: ai

    character*(4), pointer    :: clabel
    character*(10), parameter :: anglabel = 'spdfghijkl'
    character*(19) :: fmt

    integer :: i,j, itype,iatom,ang,nAngmom,seg,nSeg,nrow,ncol,k,llen
    real(realk), pointer :: Exponents(:), contrCoeffs(:)

    call trilevel_atominfo_init(ai,ls,6)




    WRITE(lo,'(A)') 'basis spherical nosegment'

    do i=1,ai%ND
      itype = ai%UATOMTYPE(i); iatom=ai%NATOM(itype)
      clabel => ls%setting%molecule(1)%p%atom(iatom)%Name
      llen   = len_trim(clabel)

         write(lo, '(2X,A,X,A)') clabel(1:llen), 'library cc-pvdz'

    enddo


    WRITE(lo,'(A)') 'end'


    call trilevel_atominfo_free(ai)


end subroutine print_nw_libbasis


subroutine print_nw_basis(ls,lo)
use trilevel_module
    implicit none
    type(lsitem)                   :: ls
    integer                        :: lo
    TYPE(trilevel_atominfo)        :: ai

    character*(4), pointer    :: clabel
    character*(10), parameter :: anglabel = 'spdfghijkl'
    character*(19) :: fmt

    integer :: i,j, itype,iatom,ang,nAngmom,seg,nSeg,nrow,ncol,k,llen
    real(realk), pointer :: Exponents(:), contrCoeffs(:)

    call trilevel_atominfo_init(ai,ls,6)




    WRITE(lo,'(A)') 'basis spherical nosegment'

    do i=1,ai%ND
      itype = ai%UATOMTYPE(i); iatom=ai%NATOM(itype)
      clabel => ls%setting%molecule(1)%p%atom(iatom)%Name
      llen   = len_trim(clabel)

      nAngmom = ls%input%BASIS%REGULAR%ATOMTYPE(itype)%nAngmom

      do ang=0, nAngmom-1

         write(lo, '(2X,A,X,A)') clabel(1:llen), anglabel(ang+1:ang+1)

         nSeg = ls%input%BASIS%REGULAR%&
                &ATOMTYPE(itype)%SHELL(ang+1)%nsegments

         do seg=1,nSeg

         nrow      = ls%input%BASIS%REGULAR%&
                &ATOMTYPE(itype)%SHELL(ang+1)%segment(seg)%nrow
         ncol       = ls%input%BASIS%REGULAR%&
                &ATOMTYPE(itype)%SHELL(ang+1)%segment(seg)%ncol

         Exponents=>ls%input%BASIS%REGULAR%&
                &ATOMTYPE(itype)%SHELL(ang+1)%segment(seg)%Exponents

         contrCoeffs=>ls%input%BASIS%REGULAR%&
                &ATOMTYPE(itype)%SHELL(ang+1)%segment(seg)%elms

         write(fmt,'(A,I2,A)') '(F18.8,',ncol,'(X,E15.8))'
        

           do k=1,nrow
           write(lo,fmt) Exponents(k),(contrCoeffs(j),j=k,k+((ncol-1)*nrow),nrow)
           enddo

         enddo


      enddo



    enddo


    WRITE(lo,'(A)') 'end'


    call trilevel_atominfo_free(ai)


end subroutine print_nw_basis


subroutine print_nw_xyz(ls,lo)
use typedef
    implicit none
    type(lsitem)                   :: ls
    integer                        :: lo

    integer :: I,J,nATOMS
    real(realk) :: ATOMXYZ(3)
    
   

    nATOMS = ls%setting%MOLECULE(1)%p%natoms

    WRITE(lo,'(A)') 'geometry units au'

    DO I=1,nATOMS !Atom coordinates
        
       DO J=1,3                                                     
          ATOMXYZ(J) = ls%setting%molecule(1)%p%atom(I)%CENTER(J)
       ENDDO

       WRITE(lo, '(A,3(X,F12.6),2X,A,X,F7.3)')  &
    &  ls%setting%molecule(1)%p%atom(I)%Name,  &
    &  ATOMXYZ, 'charge', &
    &  ls%setting%molecule(1)%p%atom(I)%Charge
      
    ENDDO

    WRITE(lo,'(A)') 'end'

end subroutine print_nw_xyz

